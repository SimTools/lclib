C   SUBROUTINE URADLS
C  
C   PURPOSE
C     MAKE A GRADIENT-SEARCH OF A MINIMUM FOR A
C        SPECIFIED FUNCTION WHICH IS NOT LINEAR IN COEFFICIENTS
C   USAGE
C     CALL MRADLS(FUNC, NTERMS, PARAM, DELTAA, CHISQR)
C   DESCRIPTION OF PARAMETERS
C     FUNC   - NAME OF THE FUNCTION TO BE MINIMIZED
C     NTERMS - NUMBER OF PARAMETERS
C     PARAM  - ARRAY OF PARAMETERS
C     DELTAA - ARRAY OF INCREMENTS FOR PARAMETERS PARAM
C     CHISQR - MINIMIZED VALUE OF THE FUNCTION
C  
      SUBROUTINE URADLS (FUNC, NTERMS, PARAM, DELTAA, CHISQR)
C  
      PARAMETER ( MXTM = 20 )
      DIMENSION PARAM(1),DELTAA(1)
      DIMENSION GRAD(MXTM)
      EXTERNAL FUNC
C  
C        EVALUATE CHI SQUARE AT BEGINNING
C  
   11 IF(NTERMS) 13, 13, 21
   13 CHISQR = 0.
      GO TO 110
   21 CONTINUE
      CHISQ1 = FUNC(NTERMS, PARAM)
C  
C        EVALUATE GRADIENT OF CHI SQUARE
C  
   31 SUM    = 0.
   32 DO 39 J = 1, NTERMS
        DELTA  = 0.1 * DELTAA(J)
        PARAM(J) = PARAM(J) + DELTA
        GRAD(J) = CHISQ1 - FUNC(NTERMS, PARAM)
        PARAM(J) = PARAM(J) - DELTA
   39 SUM    = SUM + GRAD(J)**2
      SQRSUM = SQRT(SUM)
   41 DO 42 J = 1, NTERMS
   42 GRAD(J) = DELTAA(J) * GRAD(J)/SQRSUM
C  
C        EVALUATE CHI SQUARE AT NEW POINT
C  
   51 DO 52 J = 1, NTERMS
   52 PARAM(J) = PARAM(J) + GRAD(J)
   53 CHISQ2 = FUNC(NTERMS, PARAM)
C  
C        MAKE SURE CHI SQUARE DECREASES
C  
   61 IF(CHISQ1 - CHISQ2) 62, 62, 71
   62 DO 64 J = 1, NTERMS
        PARAM(J) = PARAM(J) - GRAD(J)
   64 GRAD(J) = GRAD(J) / 2.
      GO TO 51
C  
C        INCREMENT PARAMETERS UNTIL CHI SQUARE STARTS TO INCREASE
C  
   71 DO 72 J = 1, NTERMS
   72 PARAM(J) = PARAM(J) + GRAD(J)
      CHISQ3 = FUNC(NTERMS, PARAM)
   76 IF(CHISQ3 - CHISQ2) 81, 91, 91
   81 CHISQ1 = CHISQ2
   82 CHISQ2 = CHISQ3
      GO TO 71
C  
C        FIND MINIMUM OF PARABOLA DEFINED BY LAST THREE POINTS
C  
   91 DELTA  = 1./(1. + (CHISQ1 - CHISQ2)/(CHISQ3 - CHISQ2)) + 0.5
      DO 93 J = 1, NTERMS
   93 PARAM(J) = PARAM(J) - DELTA*GRAD(J)
      CHISQR = FUNC(NTERMS, PARAM)
  101 IF(CHISQ2 - CHISQR) 102, 110, 110
  102 DO 103 J = 1, NTERMS
  103 PARAM(J) = PARAM(J) + (DELTA - 1.)*GRAD(J)
      CHISQR = FUNC(NTERMS, PARAM)
  110 CONTINUE
      RETURN
      END
