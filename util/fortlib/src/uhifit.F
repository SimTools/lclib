C   SUBROUTINE UHIFIT
C  
C   PURPOSE
C     MAKE A MINIMUM SEARCH OF A NON-LINEAR FUNCTION
C        WITH A PARABOLIC EXPANSION
C   USAGE
C     CALL MHIFIT(FUNC, NTERMS, PARAM, DELTAA, SIGMAA, CHISQR)
C   DESCRIPTION OF PARAMETERS
C     FUNC   - NAME OF THE FUNCTION TO BE MINIMIZED
C     NTERMS - NUMBER OF PARAMETERS
C     PARAM  - ARRAY OF PARAMETERS
C     DELTAA - ARRAY OF INCREMENTS FOR PARAMETERS PARAM
C     SIGMAA - ARRAY OF STANDARD DEVIATIONS FOR PARAMETERS PARAM
C     CHISQR - MINIMIZED VALUE OF THE FUNCTION
C SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C     MATINV(ARRAY, NTERMS, DET)
C        INVERTS A SYMMETRIC TWO-DIMENSIONAL MATRIX OF DEGREE NTERMS
C        AND CALCULATES ITS DETERMINANT
C  
      SUBROUTINE UHIFIT (FUNC, NTERMS, PARAM, DELTAA, SIGMAA, CHISQR)
C  
      PARAMETER ( MXTM = 10 )
      DOUBLE PRECISION ALPHA
      DIMENSION ALPHA(MXTM,MXTM), BETA(MXTM), DA(MXTM)
      DIMENSION PARAM(1),DELTAA(1), SIGMAA(1)
      EXTERNAL FUNC
C  
C        EVALUATE CHI SQUARE AT BEGINNING
C  
   11 IF(NTERMS) 14, 14, 16
   14 CHISQR = 0.
      GO TO 120
   16 CONTINUE
      CHISQ1 = FUNC(NTERMS, PARAM)
C  
C        EVALUATE ALPHA AND BETA MATRICES
C  
   20 DO 60 J = 1, NTERMS
C  
C        PARAM(J) + DELTAA(J)
C  
   21   AJ     = PARAM(J)
        PARAM(J) = AJ + DELTAA(J)
        CHISQ2 = FUNC(NTERMS, PARAM)
        ALPHA(J,J) = CHISQ2 - 2.*CHISQ1
        BETA(J) = -CHISQ2
   31   DO 50 K = 1, NTERMS
          IF (K - J) 33, 50, 36
   33     ALPHA(K,J) = (ALPHA(K,J) - CHISQ2) / 2.
          ALPHA(J,K) = ALPHA(K,J)
          GO TO 50
   36     ALPHA(J,K) = CHISQ1 - CHISQ2
C  
C        PARAM(J) + DELTAA(J) AND PARAM(K) + DELTAA(K)
C  
   41     AK     = PARAM(K)
          PARAM(K) = AK + DELTAA(K)
          CHISQ3 = FUNC(NTERMS, PARAM)
          ALPHA(J,K) = ALPHA(J,K) + CHISQ3
          PARAM(K) = AK
   50   CONTINUE
C  
C        PARAM(J) - DELTAA(J)
C  
   51   PARAM(J) = AJ - DELTAA(J)
        CHISQ3 = FUNC(NTERMS, PARAM)
        PARAM(J) = AJ
        ALPHA(J,J) = (ALPHA(J,J) + CHISQ3) / 2.
        BETA(J) = (BETA(J) + CHISQ3) / 4.
   60 CONTINUE
C  
C        ELIMINATE NEGATIVE CURVATURE
C  
   61 DO 70 J = 1, NTERMS
        IF (ALPHA(J,J)) 63, 65, 70
   63   ALPHA(J,J) = -ALPHA(J,J)
        GO TO 66
   65   ALPHA(J,J) = 0.01
   66   DO 69 K = 1, NTERMS
          IF (K - J) 68, 69, 68
   68     ALPHA(J,K) = 0.
          ALPHA(K,J) = 0.
   69   CONTINUE
   70 CONTINUE
C  
C        INVERT MATRIX AND EVALUATE PARAMETER INCREMENTS
C  
   71 CALL MATINV(ALPHA, NTERMS, DET)
      DO 76 J = 1, NTERMS
        DA(J) = 0.
   74   DO 75 K = 1, NTERMS
   75   DA(J) = DA(J) + BETA(K)*ALPHA(J,K)
   76 DA(J) = 0.2 * DA(J) * DELTAA(J)
C  
C        MAKE SURE CHI SQUARE DECREASES
C  
   81 DO 82 J = 1, NTERMS
   82 PARAM(J) = PARAM(J) + DA(J)
   83 CONTINUE
      CHISQ2 = FUNC(NTERMS, PARAM)
      IF (CHISQ1 - CHISQ2) 87, 91, 91
   87 DO 89 J = 1, NTERMS
        DA(J) = DA(J)/2.
   89 PARAM(J) = PARAM(J) - DA(J)
      GO TO 83
C  
C        INCREMENT PARAMETERS UNTIL CHI SQUARE STARTS TO INCREASE
C  
   91 DO 92 J = 1, NTERMS
   92 PARAM(J) = PARAM(J) + DA(J)
      CHISQ3 = FUNC(NTERMS, PARAM)
      IF(CHISQ3 - CHISQ2) 97, 101, 101
   97 CHISQ1 = CHISQ2
      CHISQ2 = CHISQ3
   99 GO TO 91
C  
C        FIND MINIMUM OF PARABOLA DEFINED BY LAST THREE POINTS
C  
  101 DELTA  = (CHISQ3 - CHISQ2)/(CHISQ3 + CHISQ1 - 2.*CHISQ2) + 0.5
C  
      FREE   = 1.
C  
      DO 104 J = 1, NTERMS
        PARAM(J) = PARAM(J) - DELTA*DA(J)
        IF(ALPHA(J,J).LT.0.) THEN
          SIGMAA(J) = - DELTAA(J) * SQRT (-FREE*ALPHA(J,J))
        ELSE
          SIGMAA(J) = DELTAA(J) * SQRT ( FREE*ALPHA(J,J))
        ENDIF
  104 CONTINUE
      CHISQR = FUNC(NTERMS, PARAM)
  111 IF(CHISQ2 - CHISQR) 112, 120, 120
  112 DO 113 J = 1, NTERMS
  113 PARAM(J) = PARAM(J) + (DELTA - 1.)*DA(J)
      CHISQR = FUNC(NTERMS, PARAM)
  120 CONTINUE
      RETURN
      END
